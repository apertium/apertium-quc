DELIMITERS = "<.>" "<!>" "<;>" "<?>" ;

SUBREADINGS = LTR ;

LIST A = adj ;
LIST N = n ;
LIST Clf = clf ;
LIST Prop = np ;
LIST Num = num ;
LIST Pr = pr ;
LIST Adv = adv ;
LIST V = v ; 
LIST Det = det ;
LIST Pron = prn ;

LIST Na = "na" ;

LIST Neg = neg ;
LIST Rel = rel ; 

LIST Poss = px1sg px2sg px3sg px1pl px2pl px3pl ;

LIST Impf = impf ; 
LIST Perf = perf ;

LIST Inf = inf ;

LIST Pro = pro ;
LIST Pers = pers ;

LIST Anti = anti ;
LIST Pass = pass ;

LIST IV = iv ;
LIST TV = tv ;

LIST Pl = pl ; 
LIST Taq = "taq" ;

LIST Dem = dem ;

LIST Dir = dir ;

LIST @det = @det ;
LIST @acl = @acl ;

LIST In = "in" ; 
LIST Chi = "chi" ;
LIST Jun = "jun" ;
LIST Ri = "ri" ;
LIST We = "we" ; 
LIST Kan = "<kan>" ;
LIST Tzij = "tzij" ;

LIST Days = "Aj" "Ajmak" "Aqʼabal" "Batzʼ" "Imox" "Iqʼ" "Ix" "Junajpu" "Kame" "Kan" "Kaweq" "Kej" "Kʼat" "Noʼj" "Qʼanil" "Tijax" "Toj" "Tzʼikin" "Tzʼiʼ"  ;

SET NOMINAL = N | Prop ;

SET FIN = Impf | Perf ;

SET NP-INTERNAL = Det | N | A | Adv | Num ;

SECTION

SELECT A IF (0C A OR N) (NOT 0 Poss) (1C N - Rel) ;

SELECT Clf IF (1C Prop) ;

SELECT Neg IF (0 Na) (1 FIN) ;

SELECT Pro IF (-1 Pers) ;

REMOVE Pers IF (0 In) (-1* In LINK -1 In + Pers) ;

REMOVE A IF (0 A OR N) (-1 Num) (1 Pr OR Rel) ; 

SELECT N IF (-1 Det) (0 N OR A) (1 Dem) ;
    ## Ruk’ taq we tzij ri’, xkik’am ri kiqasna’ pa ri ub’i ri Ajawxel Jesus;

#SELECT Foc IF (1 Det LINK 1 N) 

SELECT A IF (0 Chi) (-1 Jun) (1 N) ;
    ## jun chi Ajawxel, jun chi qakojonik, jun chi qaqasnaʼ

SELECT Det IF (0 We) (1 N) ;
    ## Ruk’ taq we tzij ri’, xkik’am ri kiqasna’ pa ri ub’i ri Ajawxel Jesus;

SELECT Det IF (0 We) (1 Jun) ;

SELECT N IF (-1 Det) (0 Tzij) (NOT 1 N) ;
    ## Ri areʼ kʼo ekijebʼ qʼapoj umiʼal che kkibʼij ri tzij jeʼ jas ri eqʼaxal utzij ri Dyos.

SELECT N IF (-1 Taq) (0 N OR A) (NOT 1 N) ;

REMOVE A IF (0 Tzij) (NOT 1 N - Rel) ;

REMOVE Days ;

SELECT Dir IF (-1 V) ;

SELECT Rel IF (0 Ri) (1C FIN) ;

REMOVE Pro IF (NEGATE 1* V) ;

REMOVE Inf IF (0 N OR Inf) (-1 A OR Det) ;

SECTION 

REMOVE Rel IF (0 Ri) ;

REMOVE Anti IF (0 IV) ;

REMOVE Pass IF (0 IV) ;

REMOVE Inf IF (0 N) ;

SECTION

MAP @det TARGET Det IF (1C Clf LINK 1 NOMINAL) ;

MAP @det TARGET Dem IF (-1C N LINK -1 Det) ;

MAP @acl TARGET V IF (-1C Ri + Rel) ;
