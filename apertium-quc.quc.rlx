DELIMITERS = "<.>" "<!>" "<;>" "<?>" ;

SUBREADINGS = LTR ;

LIST A = adj ;
LIST N = n ;
LIST Clf = clf ;
LIST Prop = np ;
LIST Num = num ;
LIST Pr = pr ;
LIST Adv = adv ;
LIST Qst = qst ;
LIST V = v ; 
LIST Det = det ;
LIST Pron = prn ;
LIST Aux = aux ;
LIST CS = cnjsub ;

LIST Na = "na" ;

LIST Neg = neg ;
LIST Rel = (prn rel) ; 
LIST Relat = rel ; 

LIST Poss = px1sg px2sg px3sg px1pl px2pl px3pl ;

LIST Impf = impf ; 
LIST Perf = perf ;

LIST Inf = inf ;

LIST Pro = pro ;
LIST Pers = pers ;

LIST Anti = anti ;
LIST Pass = pass ;

LIST IV = iv ;
LIST TV = tv ;

LIST Pl = pl ; 
LIST Taq = "taq" ;

LIST Dem = dem ;

LIST Dir = dir ;

LIST @det = @det ;
LIST @acl = @acl ;

LIST Ajpu = "Ajpu" ;
LIST In = "in" ; 
LIST Chi = "chi" ;
LIST Jun = "jun" ;
LIST La = "la" ;
LIST Ri = "ri" ;
LIST We = "we" ; 
LIST Kan = "<kan>" ;
LIST Tzij = "tzij" ;
LIST Sibʼalaj = "sibʼalaj" ;

LIST Sg1S = s_sg1 ;
LIST Sg2S = s_sg2 ;
LIST Sg3S = s_sg3 ;
LIST Pl1S = s_pl1 ;
LIST Pl2S = s_pl2 ;
LIST Pl3S = s_pl3 ;

LIST Sg1O = o_sg1 ;
LIST Sg2O = o_sg2 ;
LIST Sg3O = o_sg3 ;
LIST Pl1O = o_pl1 ;
LIST Pl2O = o_pl2 ;
LIST Pl3O = o_pl3 ;

LIST Days = "Aj" "Ajmak" "Aqʼabal" "Batzʼ" "Imox" "Iqʼ" "Ix" "Junajpu" "Kame" "Kan" "Kaweq" "Kej" "Kʼat" "Noʼj" "Qʼanil" "Tijax" "Toj" "Tzʼikin" "Tzʼiʼ"  ;

SET NOMINAL = N | Prop ;

SET FIN = Impf | Perf ;

SET NP-INTERNAL = Det | N | A | Adv | Num ;

SET AGR-S = Sg1S | Sg2S | Sg3S | Pl1S | Pl2S | Pl3S ;

SET AGR-O = Sg1O | Sg2O | Sg3O | Pl1O | Pl2O | Pl3O ;

SECTION

SELECT A IF (0C A OR N) (NOT 0 Poss) (1C N - Relat) ;

SELECT Clf IF (1C Prop) ;

SELECT Neg IF (0 Na) (1 FIN) ;

SELECT Pro IF (-1 Pers) ;

REMOVE Pers IF (0 In) (-1* In LINK -1 In + Pers) ;

REMOVE A IF (0 A OR N) (-1 Num) (1 Pr OR Relat) ; 

SELECT N IF (-1 Det) (0 N OR A) (1 Dem) ;
    ## Ruk’ taq we tzij ri’, xkik’am ri kiqasna’ pa ri ub’i ri Ajawxel Jesus;

#SELECT Foc IF (1 Det LINK 1 N) 

SELECT A IF (0 Chi) (-1 Jun) (1 N) ;
    ## jun chi Ajawxel, jun chi qakojonik, jun chi qaqasnaʼ

SELECT Det IF (0 We) (1 N) ;
    ## Ruk’ taq we tzij ri’, xkik’am ri kiqasna’ pa ri ub’i ri Ajawxel Jesus;

SELECT Det IF (0 We) (1 Jun) ;

SELECT N IF (-1 Det) (0 Tzij) (NOT 1 N) ;
    ## Ri areʼ kʼo ekijebʼ qʼapoj umiʼal che kkibʼij ri tzij jeʼ jas ri eqʼaxal utzij ri Dyos.

SELECT N IF (-1 Taq) (0 N OR A) (NOT 1 N) ;

REMOVE A IF (0 Tzij) (NOT 1 N - Relat) ;

REMOVE Days ;

SELECT Dir IF (-1 V) ;

SELECT Rel IF (0 Rel) (1C FIN) ;

SELECT Rel IF (1C Aux LINK 1 FIN) ;

REMOVE Pro IF (NEGATE 1* V) ;

REMOVE Inf IF (0 N OR Inf) (-1 A OR Det) ;

SELECT Aux IF (1C V) ;

SELECT Adv IF (0 Sibʼalaj) (1C A) ;

REMOVE Pr IF (-1C Ri) ;
    ## Xujkʼojiʼ chi uchiʼ ri cho.

REMOVE A IF (-1C Ri) (NOT 1 N OR Pl LINK 1 N) ;
    ## Xujkʼojiʼ chi uchiʼ ri cho.

REMOVE CS IF (0 Chi) (NEGATE 1* V) ;

SELECT N IF (-1 Det) (0C A OR N) (NOT 1 N OR Pl) ;
    ## Ri at, chachʼijaʼ bʼaʼ ri kʼax jeriʼ jas ronojel winaq che rajchʼoj ri Kristo Jesus.

REMOVE V IF (0 V OR N) (-1C Det) (1 FIN) ;
    ## Ri kinan xubʼij chike: 

REMOVE Prop IF (0 Jun) (NOT 1 Ajpu) (NOT 1 Jun LINK 1 Ajpu) ;

REMOVE Clf (NOT 1 NOMINAL) ;

SECTION 

REMOVE $$AGR-S IF (-1C AGR-S - $$AGR-S) ;

SELECT N IF (-1C Det) (0C N OR FIN) ;

SELECT Qst IF (0 La) ;

REMOVE Prop IF (0 Jun) ;

REMOVE Rel IF (0 Rel) ;

REMOVE Anti IF (0 IV) ;

REMOVE Pass IF (0 IV) ;

REMOVE Inf IF (0 N) ;

SECTION

MAP @det TARGET Det IF (1C Clf LINK 1 NOMINAL) ;

MAP @det TARGET Dem IF (-1C N LINK -1 Det) ;

MAP @acl TARGET V IF (-1C Rel) ;
